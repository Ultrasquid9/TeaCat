# Comments use hashtags

<#
	Multi-line comments use <# and #>

	<# they can even be nested! #>
#>

# Variables
&hello_world := Hello, World!;

# Just about anything can be assigned to a variable
&title := :title[
	My Webpage
];

<#
 Macros

 Accept variables as arguments, allowing for complex repeated structures.
#>
macr @person{&name &pronouns}[
	&c := Hello;

	&c my name is &name and my pronouns are &pronouns 
]

:head[
	# An & symbol allows you to access a variable
	&title
]

:body[
	:p[
		# A backslash escapes the following character
		\&title # will print "&title" in the generated HTML

		:br;

		&name := Juni;

		# Calling a macro
		@person[
			&name; # If the variable already exists, you don't need to reassign it. 
			&pronouns := she/her;
		]

		:br;

		&link := "https://www.w3schools.com/images/w3schools_green.jpg";

		# Use curly braces for tag attributes
		:img{
			src:"https://www.w3schools.com/images/w3schools_green.jpg"
			alt:"Test Image"
		};

		:ul[
			# Arrays use curly braces too 
			{
				Hello; 
				World; 
				this is; 
				a test of; 
				arrays in TeaCat;

				:h2[Tags can be placed within arrays too];
			}
		]
	]
]
